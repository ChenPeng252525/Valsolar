<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="refreshStatic">
    <Locals>
      <Local name="l_sMenu" type="string" xsi:type="array" size="1" />
      <Local name="i" type="num" xsi:type="array" size="1" />
      <Local name="l_sMessage" type="string" xsi:type="array" size="1" />
      <Local name="l_sName" type="string" xsi:type="array" size="1" />
      <Local name="l_sItem" type="string" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  cls()
  //
  // set the view of the page..
  //
  // SET THE PAGE NUMBER
  use:nPage=10000
  // USE <>PROFILE for <>PAGE
  use:nPage=use:nPage+abs(limit(saveData:mode,0,2)-2)
  // load the menu page
  if saveData:language!="japanese"
    // SET THE VIEW OF THE PAGE
    call language:page(l_sMessage,use:nPage)
    // define the menu
    call use:stringToArray(l_sMessage,sMenu)
  else
    // japanese use the array for the menu
    call language:page(sMenu8,use:nPage)
    call use:stringCopy(sMenu8,sMenu) 
  endIf
  //
  // polymorphism
  //
  // get the user Menu or special one
  call trajType:getFunctionView(sMenuUser)
  // OVERRIDE
  for i=0 to size(sMenuUser)-1
    if sMenuUser[i]!=""
      sMenu[i]=sMenuUser[i]
    endIf
  endFor
  // disable the frame
  if saveData:sTrajTypeUsing=="TRACK"
    call language:message(l_sItem,124)
    sMenu[2]=l_sItem
  endIf
    // check for the here if not in cycle
  if cycleStatus:bCycle or cycleStatus:bSimulation
    sMenu[0]=""
  endIf
  // set the menu
  if saveData:language!="japanese"
    call use:setMenu(sMenu,true,3)
  else
    call use:setMenu(sMenu,false,3)
  endIf
  call use:getMenu(sMenu,bButton)
  //
  // CREATE THE LIST
  //
  // define your list size for default
  call use:setSizeList(19,0,19,10)
  call use:setView(bLarge)
  //
  if bLarge
    call use:listcreate(saveData:sTrajTypeUsing+" "+sTrajName+"├─┤action list")
  else
    // divide in two
    if len(sTrajName)>12
      l_sName=left(sTrajName,9)+"¬"+right(sTrajName,2)
    else
      l_sName=sTrajName
    endIf
    call use:listcreate(saveData:sTrajTypeUsing+" "+l_sName)
  endIf
  //
  call use:listadd(sList)
  //
  // refresh the cursor
  call use:selectNewField(nSelect,nIndex,nView)
  // TOOL - FRAME
  if !bLarge
    call trajType:getTool(i)
    call language:message(l_sMessage,124)
    call tcp:getName(l_sName,i)
    call use:type(0,0,l_sMessage+":"+toString("",i)+" "+l_sName)
    call trajType:getFrameId(i)
    call language:message(l_sMessage,124)
    l_sName=saveData:fFrameItem[i].sName
    call use:type(0,1,"Frame:"+toString("",i)+" "+l_sName)
  endIf
end]]></Code>
  </Program>
</Programs>