<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="main" access="public">
    <Locals>
      <Local name="l_bSave" type="bool" xsi:type="array" size="1" />
      <Local name="l_nIndex" type="num" xsi:type="array" size="1" />
      <Local name="l_sEvent" type="string" xsi:type="array" size="1" />
      <Local name="l_bFound" type="bool" xsi:type="array" size="1" />
      <Local name="l_iInstr" type="InstrItem" xsi:type="array" size="1" />
      <Local name="l_sPath" type="string" xsi:type="array" size="1" />
      <Local name="l_bUnreached" type="bool" xsi:type="array" size="1" />
      <Local name="l_sMessage" type="string" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  // INIT
  call onStart()
  // LOOP UNTIL nKey=255
  do
    // refresh
    call refresh()
    nKey=getKey()
    call use:listevent(nKey,nId,sEvent)
    nSelected=nId
    nView=use:nView
    // get the index
    if find(left(sEvent,1)," ")!=-1
      l_sEvent=right(sEvent,len(sEvent)-1)
    else
      l_sEvent=sEvent
    endIf
    toNum(l_sEvent,l_nIndex,l_bFound)
    if l_bFound
      nIndex=l_nIndex-1
    endIf
    // check for the moveHold
    call prog:getItem(l_iInstr,nIndex)
    call saveData:getPathInstr(l_iInstr.sType,l_sPath)
    if find(l_sPath,"trajectory")!=-1
      call goTo(l_bUnreached,l_iInstr)
      if l_bUnreached 
        // show the message out of range
        call language:message(l_sMessage,-5)
        call use:timerDialog(l_sMessage,1)
        l_bUnreached=false
      endIf
    endIf
    // key capture
    switch nKey
      case 259
        // TAB-> ENLARGE
        bLarge=!bLarge
        call use:setView(bLarge)
      break
      case 263
        // backSpace
        if l_bFound
          call onBackEvent(l_bSave)
        endIf
      break   
      case 271
        // Simulation
        if l_bFound
          call onF1Event()
        endIf
      break
      case 272
        call onF2Event(l_bSave)
      break
      case 273
        // INPUT OUTPUT
        call onF3Event(l_bSave)
      break
      case 274
        // PASTE
        call onF4Event(l_bSave)
      break
      case 275
        // SELECT_UNSELECT
        if l_bFound
          call onF5Event()
        endIf
      break
      case 276
        // DELETE
        if l_bFound
          call onF6Event(l_bSave)
        endIf
      break
      case 277
        //INSERT
        if l_bFound or l_sEvent==""
          call onF7Event(l_bSave)
        endIf
      break
      case 278
        // SAVE
        call onF8Event(l_bSave)
      break
      case 270
        if l_bFound
          // EDIT THE FIELD
          call onEnterEvent(l_bSave)
        endIf
      break
      case 255
        // exit??
        // on Exit
        call onExit(l_bSave)
      break
    endSwitch
    //release the resource
    delay(0.1)
  until bExit
  //
  // reset
  //
  bExit=false
end]]></Code>
  </Program>
</Programs>