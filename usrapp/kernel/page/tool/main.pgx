<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="main" access="public">
    <Locals>
      <Local name="l_nSize" type="num" xsi:type="array" size="1" />
      <Local name="l_bSave" type="bool" xsi:type="array" size="1" />
      <Local name="l_nStatus" type="num" xsi:type="array" size="1" />
      <Local name="l_bToReset" type="bool" xsi:type="array" size="1" />
      <Local name="l_bCalcolate" type="bool" xsi:type="array" size="1" />
      <Local name="l_fNewWorld" type="frame" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  // INIT
  call onStart()
  // LOOP UNTIL nKey=255
  do
    call tcp:getSize(l_nSize)
    call tcp:getTool(tTool,nIndexTool)
    call refresh()
    if l_bCalcolate
      fFrame.trsf=saveData:fFrameItem[saveData:nFrameActive].fFrame.trsf
      $jogContext(tTool,fFrame,0)
      l_bCalcolate=false
    endIf
    nKey=get()
    // tool setting
    if !cycleStatus:bCycle
      if !(cycleStatus:nWorkingMode==1 and (cycleStatus:nWorkingStatus==6 or cycleStatus:nWorkingStatus==0))
        l_bToReset=true
      else
        if l_bToReset
          resetMotion()
          l_bToReset=false
        endIf
      endIf
    endIf
    // list....
    call use:listevent(nKey,nIndex,sEvent)
    nView=use:nView
    switch nKey
      case 263
        // BACKSPACE
        call onBackSpace(tTool)
        l_bCalcolate=true
        l_bSave=true
      break
      case 264
        // LEFT CLICK -1
        if nIndexTool>0
          nIndexTool=nIndexTool-1
          l_bSave=true
          l_bCalcolate=true
        endIf
      break
      case 268
        // RIGHT CLICK +1
        if nIndexTool<l_nSize-1
          nIndexTool=nIndexTool+1
          l_bSave=true
          l_bCalcolate=true
        endIf
      break
      case 271
        call onF1Event()
      break
      case 272
        call onF2Event()
      break
      case 273
        // HERE
        call onF3Event(l_bSave)
      break
      case 274
        // COMPUTE
        call onF4Event(l_bSave)
        l_bCalcolate=true
      break
      case 275
        // MOVE
        call onF5Event()
      break
      case 276
        // DUPLICATE
        call onF6Event(tTool,l_bSave)
      break
      case 277
        // DELETE
        call onF7Event(l_bSave)
      break
      case 278
        // NEW
        call onF8Event(l_bSave)
      break
      case 270
        // USE ALSO BACKSPACE
        // EDIT FIELD
        call onEnterEvent(tTool)
        l_bCalcolate=true
        l_bSave=true
      break
    endSwitch
  until nKey==255
  // on Exit
  fFrame.trsf=saveData:fFrameItem[saveData:nFrameActive].fFrame.trsf
  $jogContext(tTool,fFrame,0)
  call onExit(l_bSave)
end

]]></Code>
  </Program>
</Programs>